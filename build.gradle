/*
 * (c) Copyright 2019 Palantir Technologies Inc. All rights reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven {
            url 'https://palantir.bintray.com/releases'
        }
    }

    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.5'
        classpath 'com.netflix.nebula:gradle-info-plugin:7.1.3'
        classpath 'com.netflix.nebula:nebula-publishing-plugin:17.2.1'
        classpath 'com.palantir.baseline:gradle-baseline-java:3.10.0'
        classpath 'com.palantir.gradle.conjure:gradle-conjure:4.23.0'
        classpath 'com.palantir.gradle.consistentversions:gradle-consistent-versions:1.17.3'
        classpath 'com.palantir.gradle.gitversion:gradle-git-version:0.12.3';
        classpath 'com.palantir.javaformat:gradle-palantir-java-format:0.3.27'
        classpath 'com.palantir.sls-packaging:gradle-sls-packaging:3.3.0'
        classpath 'gradle.plugin.org.inferred:gradle-processors:3.3.0'
    }
}

plugins {
    id 'com.palantir.git-version' version '0.12.3'
}

apply plugin: 'com.palantir.baseline'
apply plugin: 'com.palantir.git-version'
apply plugin: 'com.palantir.consistent-versions'

version gitVersion()

allprojects {
    apply plugin: 'com.palantir.java-format'
    version rootProject.version
    group 'com.palantir.spark.tpcds'

    repositories {
        jcenter()
        mavenCentral()
        maven {
            url 'https://palantir.bintray.com/releases'
        }
    }
    // Configure BOMs as rootConfiguration dependencies
    dependencies {
        rootConfiguration platform("org.apache.spark:spark-dist_2.11-hadoop-palantir-bom")
    }
}

configure(subprojects) {
    apply plugin: 'java-library'
    apply plugin: 'org.inferred.processors'

    tasks.withType(JavaCompile) {
        options.compilerArgs += ['-Werror']
    }

    tasks.check.dependsOn(javadoc)
    sourceCompatibility = 1.8
}
